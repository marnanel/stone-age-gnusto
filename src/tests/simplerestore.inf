! Gnusto tests: simple save/restore test
! Copyright (c) 2003 Thomas Thurman. Distributed under v2 of the GNU GPL.
!
! Note:
!  This file can't be part of the final version of Gnusto, since
!  (obviously) it depends on Inform, and Inform is nonfree, which
!  will keep Gnusto out of the main parts of some distributions
!  (notably Debian). I'm figuring out a solution to this, but for
!  now this file can stay here.
!
! This program accepts an infinite series of keystrokes. It keeps
! a value stored in a variable. If the keystroke is:
!    "."       it will print the contents of the variable
!    "S"       it will save
!    "R"       it will save
!    Otherwise it will set the variable to the keystroke and print it.

Release 177;

! "register" is a global because of an infelicity in the
! design of Quetzal: local variables of the bootstrap function
! (the one first on the call stack) are not saved. This is OK
! for most Z-code programs because the bootstrap function
! traditionally calls something more complicated and then quits,
! but it does mean that the simplest programs, like this one,
! can't expect to keep their locals across a save.
Global register;

[ Main keypress result;

    .top;

    @read_char keypress;

    @je keypress 46 ?do_fullstop; ! .
    @je keypress 83 ?do_save;     ! S
    @je keypress 82 ?do_restore;  ! R
    @je keypress 85 ?do_undo;     ! U
    
    @add keypress 0 -> register;
    @print_char register;
    @print_char 32;
    
    @je 0 0 ?top;

    .do_fullstop;
    @print_char register;
    @print_char 32;
    @je 0 0 ?top;

    .do_save;
    @save -> result;
    @je result 0 ?fail;
    @je 0 0 ?top;
    
    .do_restore;
    @restore -> result;
    @je 0 0 ?fail;

    .do_undo;
    @print "(undo test not implemented)";
    @je 0 0 ?top;

    .fail;
    @print_ret "*FAIL Unexpected error: quitting";
];

