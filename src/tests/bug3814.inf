! Gnusto tests: terminating characters
! Copyright (c) 2003 Thomas Thurman. Distributed under v2 of the GNU GPL.
!
! Note:
!  This file can't be part of the final version of Gnusto, since
!  (obviously) it depends on Inform, and Inform is nonfree, which
!  will keep Gnusto out of the main parts of some distributions
!  (notably Debian). I'm figuring out a solution to this, but for
!  now this file can stay here.
!
Release 3814;

! We can't change the terminating chars list on the fly, because
! some terps (e.g. Frotz) read the address out of the header on
! startup and cache it. We have to set the buffer to be really
! really long, and then store where Inform puts it.
Zcharacter terminating 129 130 131 132 133 134 135 136 137 138 139 140
    141 142 143 144 145 146 147 148 149 150;

Array textbuffer -> 100;
Global terms;
Global termcount;


[AddTerminator which banana;
    print "Adding terminator ",which," at ",terms,"+",termcount,".^";
    @storeb terms termcount which;
    @loadb terms termcount -> banana;
    print "(stored as ",banana,")^";
    
    termcount++;
    
];


[Main keypressed i temp;

    @loadw 46 0 -> terms;
    
    while(1) 
    {

    	@print "Welcome to the test for bug 3814. This test should run
	    interactively.^^Please enter a string of characters
	    representing the keys which should stop the next read
	    (not this one). Choose from:^^^
	    
	    1-9 = F1 to F9^
	    ABC = F10, F11, F12^
	    *   = any F-key (wildcard)^
	    Z   = letter Z -- should not be honoured by the terp^
	    UDLR= up, down, left, right cursor keys^
	    M   = mouse click^
	    .   = quit this program^?";
    
	
    	@storeb textbuffer 0 98;
    	@storeb textbuffer 1 0;
    	@aread textbuffer 0 -> keypressed;

	termcount = 0;
	
    	for (i=0: i<textbuffer->1: i++) 
    	{
	    temp = textbuffer->(i+2);
	    
	    if (temp == 46) {
		! full stop
	    	rtrue;
	    }
	    else if (temp == 122) { AddTerminator( 90); } !letter Z
	    else if (temp == 117) { AddTerminator(129); } !up
	    else if (temp == 100) { AddTerminator(130); } !down
	    else if (temp == 108) { AddTerminator(131); } !left
	    else if (temp == 114) { AddTerminator(132); } !right
	    else if (temp>=49 && temp<=57) { AddTerminator(temp+84); } !F1-F9
	    else if (temp>=97 && temp<=99) { AddTerminator(temp+45); } !F10-F12
	    else if (temp == 109) { AddTerminator(254); } !mouse click
	    else if (temp ==  42) { AddTerminator(255); } !any F-key
	    else { print "Sorry, I don't know the code ",temp,".^"; }
	}
	AddTerminator(0); ! end of the list

	print "^^Now enter any string. The characters you chose should
	    terminate it. (Newline still works too, of course.)^>";

    	@storeb textbuffer 0 98;
    	@storeb textbuffer 1 0;
    	@aread textbuffer 0 -> keypressed;

	if (keypressed==10) 
	{
	    print "^You pressed Enter.^^";
	}
	else if (keypressed==13) 
	{
	    print "^You pressed Enter, and your Z-machine ignores the
		recommendation that Enter is 10. Please LART the author.^^";
	}
	else
	{
	    print "^You pressed ",keypressed,".^";
	}

	print
	    "^^================================================================^^";
	
    }
    
];
